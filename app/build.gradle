//apply plugin: 'com.android.application'
//
//apply plugin: 'kotlin-android'
//
//apply plugin: 'kotlin-kapt'
//
//apply plugin: 'jacoco'
//
//apply plugin: 'com.google.android.gms.oss-licenses-plugin'
//
//apply plugin: 'deploygate'
//
//apply plugin: "com.google.gms.google-services"
//// Add the Crashlytics Gradle plugin
//apply plugin: 'com.google.firebase.crashlytics'
//
plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'jacoco'
    id 'com.google.android.gms.oss-licenses-plugin'
    id 'deploygate'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
}

jacoco {
    toolVersion = "0.8.8"
    reportsDirectory = layout.buildDirectory.dir('customJacocoReportDir')
}
task jacocoTestReport(type: JacocoReport) {
    dependsOn("createDebugCoverageReport")
    dependsOn("testDebugUnitTest")
    group = "testing"
    //無視するファイル(excludes)の設定を行います
    def fileFilter = ['**/R.class',
                      '**/R$*.class',
                      '**/BuildConfig.*',
                      '**/Manifest*.*',
                      'android/**/*.*',
                      'androidx/**/*.*',
                      '**/Lambda$*.class',
                      '**/Lambda.class',
                      '**/*Lambda.class',
                      '**/*Lambda*.class',
                      '**/*Lambda*.*',
                      '**/*Builder.*'
    ]
    def javaDebugTree = fileTree(dir: "${buildDir}/intermediates/javac/debug/classes", excludes: fileFilter)
    def kotlinDebugTree = fileTree(dir: "${buildDir}/tmp/kotlin-classes/debug", excludes: fileFilter)
    def mainSrc = "${project.projectDir}/src/main/java"

    getSourceDirectories().setFrom(files([mainSrc]))
    //Java, Kotlin混在ファイル対応
    getClassDirectories().setFrom(files([javaDebugTree, kotlinDebugTree]))
    getExecutionData().setFrom(fileTree(dir: project.projectDir, includes: [
            '**/*.exec',    //JUnit Test Result
            '**/*.ec']))     //Espresso Test Result
}

def mainVersionName = "1.5"
def getVersionCode = { ->
    return project.hasProperty('versionCode') ? versionCode.toInteger() : 1
}
def getVersionName = { ->
    return project.hasProperty('endOfVersionName') ? mainVersionName+"."+endOfVersionName : mainVersionName
}
android {
    compileSdkVersion 33
    buildToolsVersion '33.0.2'
    defaultConfig {
        applicationId "net.my.throwtrash"
        minSdkVersion 21
        targetSdkVersion 33
        versionCode getVersionCode()
        versionName getVersionName()
        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        testInstrumentationRunnerArguments clearPackageData: 'true'
        multiDexEnabled true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    signingConfigs {
        release {
            if(project.rootProject.file("local.properties").exists()) {
                def properties = new Properties()
                properties.load(project.rootProject.file("local.properties").newDataInputStream())
                storeFile file(properties.getProperty("keystore.file"))
                storePassword properties.get("keystore.storepwd")
                keyAlias 'key0'
                keyPassword properties.get("keystore.keypwd")
            }
        }
    }
    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
        }
        debug {
            applicationIdSuffix ".debug"
            debuggable true
        }
    }
    flavorDimensions 'mode'
    productFlavors {
        local {
            dimension 'mode'
        }
        publish {
            dimension 'mode'
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    testOptions {
        unitTests.returnDefaultValues = true
        execution 'ANDROIDX_TEST_ORCHESTRATOR'
        unitTests.all {
            useJUnitPlatform()
        }
    }
    buildFeatures {
        viewBinding true
    }
    kotlinOptions {
        jvmTarget = "17"
    }
    bundle {
        storeArchive {
            enable = false
        }
    }
    namespace 'net.mythrowaway.app'
    compileOptions {
        targetCompatibility JavaVersion.VERSION_17
        sourceCompatibility JavaVersion.VERSION_17
    }
}
// 設定は任意です
deploygate {
    // 認証情報を指定することができます
    appOwnerName = "quotto"
    apiToken = project.hasProperty('deploygateToken') ? deploygateToken : System.getenv("DEPLOYGATE_TOKEN")
}
dependencies {
    implementation 'androidx.test.espresso:espresso-contrib:3.5.1'
    def lifecycle_version = '2.6.1'

    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.core:core-ktx:1.10.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.3.1'
    implementation 'androidx.drawerlayout:drawerlayout:1.2.0'
    // ViewModel
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycle_version"
    // LiveData
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    implementation 'com.fasterxml.jackson.module:jackson-module-kotlin:2.14.2'
    implementation 'com.google.android.material:material:1.9.0'
    implementation 'androidx.preference:preference-ktx:1.2.1'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.7.3'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.7.3'
    implementation 'com.github.kittinunf.fuel:fuel:2.3.1'
    implementation 'com.github.kittinunf.fuel:fuel-json:2.3.1'
    implementation 'com.github.kittinunf.fuel:fuel-android:2.3.1'
    implementation 'com.google.android.play:core-ktx:1.8.1'
    implementation 'com.google.dagger:dagger:2.48'
    kapt 'com.google.dagger:dagger-compiler:2.48'

    implementation 'com.google.android.gms:play-services-oss-licenses:17.0.1'
    // https://mvnrepository.com/artifact/commons-net/commons-net
    implementation group: 'commons-net', name: 'commons-net', version: '20030805.205232'

    testImplementation "com.nhaarman:mockito-kotlin:1.6.0"
    testImplementation 'org.mockito:mockito-core:5.5.0'
    testImplementation 'org.mockito:mockito-inline:5.2.0'
    testImplementation 'org.mockito.kotlin:mockito-kotlin:3.2.0'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.9.3'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.3'
    testRuntimeOnly 'org.junit.vintage:junit-vintage-engine:5.9.3'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestUtil 'androidx.test:orchestrator:1.4.2'

    // Firebase Crashlytics
    implementation platform('com.google.firebase:firebase-bom:32.1.0')
    implementation "com.google.firebase:firebase-analytics"
    implementation 'com.google.firebase:firebase-crashlytics-ktx'
    implementation 'com.google.firebase:firebase-analytics-ktx'
}